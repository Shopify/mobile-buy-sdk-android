apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def VERSION_NAME = '1.3.0'

// Params for bintrayUpload
ext {
    bintrayOrg = 'shopify'
    bintrayRepo = 'shopify-android'
    bintrayName = 'mobile-buy-sdk'

    publishedGroupId = 'com.shopify.mobilebuysdk'
    libraryName = 'MobileBuySDK'
    artifact = 'buy'

    libraryDescription = 'Shopify\'s Mobile Buy SDK makes it simple to sell physical products inside your mobile app.'

    siteUrl = 'https://github.com/Shopify/mobile-buy-sdk-android'
    gitUrl = 'https://github.com/Shopify/mobile-buy-sdk-android.git'

    libraryVersion = VERSION_NAME

    licenseName = 'The MIT License'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    com.android.ddmlib.DdmPreferences.setTimeOut(60000) // https://code.google.com/p/android/issues/detail?id=104305

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionName VERSION_NAME
        version VERSION_NAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField 'okhttp3.logging.HttpLoggingInterceptor.Level', 'OKHTTP_LOG_LEVEL', 'okhttp3.logging.HttpLoggingInterceptor.Level.BODY'

            // for legacy http support
            useLibrary 'org.apache.http.legacy'

            // We pull the shop settings either from the Environment or a file called shop.properties at the root of the project.
            // Environment variables will take precedence, we will attempt to read those first.
            def shopDomain
            def apiKey
            def appId
            def useMockResponses
            def androidPayPublicKey

            // get the shop setting from the environment if there any
            shopDomain = System.getenv("SHOP_DOMAIN")
            apiKey = System.getenv("API_KEY")
            appId = System.getenv("APP_ID")
            androidPayPublicKey = System.getenv("ANDROID_PAY_PUBLIC_KEY")

            // pull the store properties from the local file if it exists.  Environment variables take precedence
            def shopProperties = new Properties();
            File shopPropertiesFile = project.file('shop.properties')

            if (shopPropertiesFile.exists()) {
                shopProperties.load(new FileInputStream(shopPropertiesFile))
                shopDomain = shopDomain ?: shopProperties["SHOP_DOMAIN"]
                apiKey = apiKey ?: shopProperties["API_KEY"]
                appId = appId ?: shopProperties["APP_ID"]
                androidPayPublicKey = androidPayPublicKey ?: shopProperties["ANDROID_PAY_PUBLIC_KEY"]
            }

            if (!shopDomain) {
                shopDomain = ""
            }
            if (!apiKey) {
                apiKey = ""
            }
            if (!appId) {
                appId = ""
            }
            if (!androidPayPublicKey) {
                androidPayPublicKey = ""
            }

            buildConfigField "String", "SHOP_DOMAIN", "\"" + shopDomain.toString() + "\""
            buildConfigField "String", "API_KEY", "\"" + apiKey.toString() + "\""
            buildConfigField "String", "APP_ID", "\"" + appId.toString() + "\""
            buildConfigField "String", "ANDROID_PAY_PUBLIC_KEY",  "\"" + androidPayPublicKey.toString() + "\""
        }

        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'okhttp3.logging.HttpLoggingInterceptor.Level', 'OKHTTP_LOG_LEVEL', 'okhttp3.logging.HttpLoggingInterceptor.Level.NONE'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/androidTest/assets/'] } }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'

    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:palette-v7:23.1.1'
    compile 'com.android.support:customtabs:23.1.1'

    compile 'com.google.android.gms:play-services-wallet:8.4.0'

    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.1'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.1'
    androidTestCompile 'com.android.support:support-annotations:23.0.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'

    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    compile 'io.reactivex:rxandroid:1.0.1'

}

sourceSets {
    javadoc {
        java.srcDirs = ['src/main/java']
    }
}

task javadoc(type: Javadoc) {
    source = sourceSets.javadoc.allSource
    title = "Mobile Buy SDK"

    exclude('**/internal/**')
    exclude('**/ui/ProductDetailsImageAreaTouchHandler.java')
    exclude('**/ui/ProductDetailsListener.java')
    exclude('**/ui/ProductVariantOptionView.java')
    exclude('**/ui/ProductImagePagerAdapter.java')
    exclude('**/ui/ScrollFlingGestureListener.java')
    exclude('**/ui/VariantSelectionController.java')
    exclude('**/ui/ViewPagerAdapter.java')
    exclude('**/customTabs/**')

    classpath = configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    def designLib = "${android.sdkDirectory}/extras/android/support/design/libs/android-support-design.jar"
    classpath += project.files(designLib)

    def supportLib = "${android.sdkDirectory}/extras/android/support/v4/android-support-v4.jar"
    classpath += project.files(supportLib)

    def playLib = "${android.sdkDirectory}/extras/google/google_play_services/libproject/google-play-services_lib/libs/google-play-services.jar"
    classpath += project.files(playLib)

    options {
        linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference")
        links("http://square.github.io/retrofit/javadoc")
    }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = 'buy'
    from javadoc.destinationDir
}

task archiveReleasePackage(type: Zip, dependsOn: [assembleRelease, javadocJar]) {
    baseName = "mobile-buy-sdk-android"

    from("./build/libs") {
        include javadocJar.getArchiveName()
    }

    from("./build/docs") {
        include 'javadoc/**'
    }

    from("./build/outputs/aar") {
        include "buy-" + version + ".aar"
    }

    from("..") {
        include "sample/src/main/**"
        include "sample/.gitignore"
        include "sample/build.gradle.packaging"
        include "sample/proguard-rules.pro"

        rename "build.gradle.packaging", "build.gradle"
    }

    from("../..") {
        include "LICENSE"
        include "NOTICE"
    }

    into(baseName + "-" + version)
}

project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = false
    }
}

/*
Great guide for publishing to JCenter:
http://inthecheesefactory.com/blog/how-to-upload-library-to-jcenter-maven-central-as-dependency/en

Also search for 'userOrg' here (we need this to publish to the Shopify organization on Bintray):
https://github.com/bintray/gradle-bintray-plugin

Steps:
gradlew install
gradlew bintrayUpload
*/

group = publishedGroupId

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

version = VERSION_NAME

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

File localProperties = project.rootProject.file('local.properties')
Properties properties = new Properties()
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = bintrayOrg
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}
