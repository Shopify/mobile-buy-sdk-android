/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2016 Shopify Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 */

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
  }
}

apply plugin: "com.android.application"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "me.tatarka.retrolambda"

android {
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  compileSdkVersion 24
  buildToolsVersion "24.0.2"

  defaultConfig {
    applicationId "com.shopify.mobilebuysdk.demo"
    minSdkVersion 14
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"
    vectorDrawables.useSupportLibrary = true
  }
  buildTypes {
    all {
      // We pull the shop settings either from the Environment or a file called shop.properties at the root of the project.
      // Environment variables will take precedence, we will attempt to read those first.
      def shopDomain
      def apiKey
      def appId
      def androidPayPublicKey

      // get the shop setting from the environment if there any
      shopDomain = System.getenv("SHOP_DOMAIN")
      apiKey = System.getenv("API_KEY")
      appId = System.getenv("APP_ID")
      androidPayPublicKey = System.getenv("ANDROID_PAY_PUBLIC_KEY")

      // pull the store properties from the local file if it exists.  Environment variables take precedence
      def shopProperties = new Properties();
      File shopPropertiesFile = project.file('shop.properties')
      if (shopPropertiesFile.exists()) {
        shopProperties.load(new FileInputStream(shopPropertiesFile))
        shopDomain = shopDomain ?: shopProperties["SHOP_DOMAIN"]
        apiKey = apiKey ?: shopProperties["API_KEY"]
        appId = appId ?: shopProperties["APP_ID"]
        androidPayPublicKey = androidPayPublicKey ?: shopProperties["ANDROID_PAY_PUBLIC_KEY"]
      }

      if (!shopDomain) {
        shopDomain = ""
      }
      if (!apiKey) {
        apiKey = ""
      }
      if (!appId) {
        appId = ""
      }
      if (!androidPayPublicKey) {
        androidPayPublicKey = ""
      }

      buildConfigField "String", "SHOP_DOMAIN", "\"" + shopDomain.toString() + "\""
      buildConfigField "String", "API_KEY", "\"" + apiKey.toString() + "\""
      buildConfigField "String", "APP_ID", "\"" + appId.toString() + "\""
      buildConfigField "String", "ANDROID_PAY_PUBLIC_KEY", "\"" + androidPayPublicKey.toString() + "\""
      buildConfigField "boolean", "USE_RX_API", "true"
    }
    debug {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }
}

retrolambda {
  jvmArgs "-noverify"
}

dependencies {
  compile fileTree(dir: "libs", include: ["*.jar"])

  compile "com.android.support:appcompat-v7:24.2.1"
  compile "com.android.support:appcompat-v7:24.2.1"
  compile "com.android.support:cardview-v7:24.2.1"
  compile "com.android.support:design:24.2.1"
  compile "com.android.support:recyclerview-v7:24.2.1"

  compile "com.facebook.fresco:fresco:0.12.0"

  compile "com.jakewharton:butterknife:8.0.1"
  apt "com.jakewharton:butterknife-compiler:8.0.1"

  compile("me.henrytao:mdcore:3.3.6") {
    exclude group: "com.android.support"
  }
  compile("me.henrytao:recycler-pager-adapter:2.0.1") {
    exclude group: "com.android.support"
  }
  compile("me.henrytao:recyclerview:3.0.7") {
    exclude group: "com.android.support"
  }
  compile("me.henrytao:rx-shared-preferences:1.3.0") {
    exclude group: "io.reactivex"
  }

  compile(project(":buy")) {
    exclude group: "com.android.support"
  }

  testCompile "junit:junit:4.12"
}
